// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract MultiSigWallet {
    // --------------------------- ğŸ— EVENT ---------------------------
    event Deposit(address indexed sender, uint amount, uint balance);
    event SubmitTransaction(
        address indexed owner,
        uint indexed txIndex,
        address indexed to,
        uint value,
        bytes data
    );
    event ConfirmTransaction(address indexed owner, uint indexed txIndex);
    event RevokeConfirmation(address indexed owner, uint indexed txIndex);
    event ExecuteTransaction(address indexed owner, uint indexed txIndex);
    // --------------------------- ğŸ— EVENT ---------------------------

    // --------------------------- ğŸ§º VARIABLES ---------------------------
    address[] public owners;                 //ì†Œìœ ìë“¤ 
    mapping(address => bool) public isOwner; //ì†Œìœ ì : boolean
    uint public numConfirmationsRequired;    //ì»¨íŒê°œìˆ˜

    //íŠ¸ëœì­ì…˜ êµ¬ì¡°ì²´ dataë¥¼ ë°”ì´íŠ¸í˜•íƒœë¡œ ë‹´ì„ìˆ˜ ìˆëŠ”ê²Œ íŠ¹ì§•
    struct Transaction {
        address to;
        uint value;
        bytes data;
        bool executed;
        uint numConfirmations;
    }

    //[íŠ¸ëœì­ì…˜ ì¸ë±ìŠ¤][ [ì†Œìœ ì ì£¼ì†Œ][ true/false ] ] ì´ì¤‘ë§µ ìë£Œêµ¬ì¡°ë¡œ íŠ¹ì • íŠ¸ëœì­ì…˜ì—ëŒ€í•´ ì†Œìœ ìê°€ ì»¨íŒ í–ˆëŠ”ì§€ ì—¬ë¶€ë¥¼ íŒë‹¨í•˜ê¸° ìœ„í•œ ë³€ìˆ˜
    mapping(uint => mapping(address => bool)) public isConfirmed;
    
    //íŠ¸ëœì­ì…˜ë“¤ 
    Transaction[] public transactions;
    // --------------------------- ğŸ§º VARIABLES ---------------------------

    //ì†Œìœ ìë§Œ í˜¸ì¶œ ê°€ëŠ¥ 
    modifier onlyOwner() {
        require(isOwner[msg.sender], "not owner");
        _;
    }

    //íŠ¸ëœì­ì…˜ ì¡´ì¬ ìœ ë¬´ ê²€ì‚¬ , revert ìƒí™©ì€ [0,1,2] ìƒí™©ì—ì„œ 5ë²ˆì§¸ íŠ¸ëœì­ì…˜ì„ ì¡°íšŒí•˜ëŠ” ê²½ìš° 
    modifier txExists(uint _txIndex) {
        require(_txIndex < transactions.length, "tx does not exist");
        _;
    }

    // íŠ¸ëœì­ì…˜ì´ ì‹¤í–‰ëœ ìƒíƒœì¸ì§€ 
    modifier notExecuted(uint _txIndex) {
        require(!transactions[_txIndex].executed, "tx already executed");
        _;
    }
    // íŠ¸ëœì­ì…˜ì´ ì»¨íŒëœ ìƒíƒœì¸ì§€ 
    modifier notConfirmed(uint _txIndex) {
        require(!isConfirmed[_txIndex][msg.sender], "tx already confirmed");
        _;
    }

    constructor(address[] memory _owners, uint _numConfirmationsRequired) {
        require(_owners.length > 0, "owners required");
        require(
            _numConfirmationsRequired > 0 &&
                _numConfirmationsRequired <= _owners.length,
            "invalid number of required confirmations"
        );

        for (uint i = 0; i < _owners.length; i++) {
            address owner = _owners[i];

            require(owner != address(0), "invalid owner");
            require(!isOwner[owner], "owner not unique");

            isOwner[owner] = true;
            owners.push(owner);
        }

        numConfirmationsRequired = _numConfirmationsRequired;
    }
    //í•´ë‹¹ ì§€ê°‘ìœ¼ë¡œ ì˜ˆì¹˜í• ê²½ìš° ì¦‰ ì´ ì»¨íŠ¸ë™íŠ¸ë¡œ ì´ë”ë¥¼ ë³´ë‚´ëŠ” ê²½ìš°
    receive() external payable {
        emit Deposit(msg.sender, msg.value, address(this).balance);
    }

    //íŠ¸ëœì­ì…˜ ì œì¶œ 
    function submitTransaction(address _to,uint _value,bytes memory _data) public onlyOwner {
        //íŠ¸ëœì­ì…˜ ì¸ë±ìŠ¤ ì €ì¥ 
        uint txIndex = transactions.length;
        //íŠ¸ëœì­ì…˜ ë°°ì—´ì— ìƒˆë¡œìš´ íŠ¸ëœì­ì…˜ ì €ì¥
        transactions.push(
            Transaction({
                to: _to,
                value: _value,
                data: _data,
                executed: false,
                numConfirmations: 0
            })
        );

        emit SubmitTransaction(msg.sender, txIndex, _to, _value, _data);
    }
    //íŠ¸ëœì­ì…˜ ì»¨íŒ ìš”ì²­í•˜ê¸° 
    //ì†Œìœ ìë§Œ í˜¸ì¶œê°€ëŠ¥, ì¡´ì¬í•˜ëŠ” íŠ¸ëœì­ì…˜ì¸ì§€, ì‹¤í–‰ë˜ì§€ ì•Šì•˜ëŠ”ì§€, ì»¨íŒì´ ì•ˆëœ íŠ¸ëœì­ì…˜ì¸ì§€ ê²€ì‚¬
    function confirmTransaction(uint _txIndex ) public onlyOwner txExists(_txIndex) notExecuted(_txIndex) notConfirmed(_txIndex) {
        Transaction storage transaction = transactions[_txIndex];
        transaction.numConfirmations += 1;
        isConfirmed[_txIndex][msg.sender] = true;

        emit ConfirmTransaction(msg.sender, _txIndex);
    }

    //íŠ¸ëœì­ì…˜ ì‹¤í–‰ 
    //íŠ¸ëœì­ì…˜ì´ ì¡´ì¬, ì‹¤í–‰ë˜ì§€ ì•Šì€ ìƒíƒœì¸ì§€ ê²€ì‚¬
    //ì„¤ì •í•œ ìš”êµ¬ ì»¨íŒìˆ˜ì— ì¶©ì¡±í•˜ëŠ”ì§€ ê²€ì‚¬ 
    function executeTransaction(uint _txIndex) public onlyOwner txExists(_txIndex) notExecuted(_txIndex) {
        Transaction storage transaction = transactions[_txIndex];

        require(
            transaction.numConfirmations >= numConfirmationsRequired,
            "cannot execute tx"
        );

        transaction.executed = true;

        (bool success, ) = transaction.to.call{value: transaction.value}(
            transaction.data
        );
        require(success, "tx failed");

        emit ExecuteTransaction(msg.sender, _txIndex);
    }
    //íŠ¸ëœì­ì…˜ ì·¨ì†Œ
    //ì†Œìœ ìë§Œ í˜¸ì¶œê°€ëŠ¥, ì¡´ì¬í•˜ëŠ” íŠ¸ëœì­ì…˜ì— ëŒ€í•œ ìš”ì²­ì¸ì§€, ì´ë¯¸ ì‹¤í–‰ëœ íŠ¸ëœì­ì…˜ì€ ì•„ë‹Œì§€ ê²€ì‚¬
    function revokeConfirmation( uint _txIndex) public onlyOwner txExists(_txIndex) notExecuted(_txIndex) {
        Transaction storage transaction = transactions[_txIndex];
        //ì´ë¯¸ ì·¨ì†Œí•œ ìƒíƒœì¸ì§€ ê²€ì‚¬
        require(isConfirmed[_txIndex][msg.sender], "tx not confirmed");
        //ì»¨íŒìˆ˜ -1
        transaction.numConfirmations -= 1;
        //í•´ë‹¹ íŠ¸ëœì­ì…˜ì˜ ì»¨íŒì„ ì·¨ì†Œìƒíƒœë¡œ ë³€ê²½
        isConfirmed[_txIndex][msg.sender] = false;
        //ì´ë²¤íŠ¸ ê¸°ë¡
        emit RevokeConfirmation(msg.sender, _txIndex);
    }
    //ì†Œìœ ìë“¤ ë°˜í™˜
    function getOwners() public view returns (address[] memory) {
        return owners;
    }
    //í˜„ì¬ ê¹Œì§€ íŠ¸ëœì­ì…˜ ìˆ˜ ë°˜í™˜
    function getTransactionCount() public view returns (uint) {
        return transactions.length;
    }
    //íŠ¸ëœì­ì…˜ ìƒì„¸ì •ë³´ ë°˜í™˜ 
    function getTransaction( uint _txIndex )public view
        returns (
            address to,
            uint value,
            bytes memory data,
            bool executed,
            uint numConfirmations
        )
    {
        Transaction storage transaction = transactions[_txIndex];

        return (
            transaction.to,
            transaction.value,
            transaction.data,
            transaction.executed,
            transaction.numConfirmations
        );
    }
}
